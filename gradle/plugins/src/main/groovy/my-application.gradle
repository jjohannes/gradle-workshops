plugins {
    id 'my-java-base'
    id 'application'
}

tasks.register("mytask", org.example.gradlebuild.MyTask) {
    verbose.set(false)
    inputFiles.from(tasks.named('jar'))
    // "long" version: inputFiles.from(tasks.named('jar').flatMap { it.destinationDirectory })
    inputFiles.from(configurations.runtimeClasspath)

    countFile.set(layout.buildDirectory.file("jar-count.txt"))
}

tasks.register("bundle", Zip) {
    group = "mygroup"
    description = "Packages all"

    from(configurations.runtimeClasspath)
    from(layout.projectDirectory.file('readme.txt'))
    from(tasks.named('jar'))

    destinationDirectory.set(layout.buildDirectory.dir("out"))
}

tasks.register("myBuild") {
    dependsOn(tasks.named("test"))
    dependsOn(tasks.named("bundle"))
}
